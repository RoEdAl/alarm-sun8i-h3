# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname='sunxi-dtbs'
_srcname=linux-5.3
pkgdesc='Additional DTBs for Allwinder-based boards'
conflicts=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
replaces=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
pkgver=5.3.p1
pkgrel=1
arch=('armv7h')
url="http://www.kernel.org/"
license=('GPL2')
depends=('linux>5.1')
makedepends=('bc' 'git')
options=('!strip')
_cfgrel=82827b2007864d911596542324e992f1cf8817ed
_patches=(
	'0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch'
	'0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch'
	'0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch'
	'0004-arm-dts-sunxi-NanoPi-Neo-add-missing-ethernet0-alias.patch'
	'0005-arm-dts-sunxi-BananaPi-M2-Zero-update-emac.patch'
	'0006-ARM-dts-sun8i-Enable-HDMI-on-Banana-Pi-M2-Zero-board.patch'
	'0007-arm-dts-sunxi-Add-Banana-Pi-P2-Zero-board.patch'
	'0008-arm-dts-sunxi-OrangePi-Zero-improve-WiFi-bindings.patch'
	'0009-arm-dts-sunxi-OrangePi-R1-remove-correct-node.patch'
)
source=("http://www.kernel.org/pub/linux/kernel/v5.x/${_srcname}.tar.xz"
	${_patches[@]}
	"config-${pkgver}::http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-armv7/config")
md5sums=('c99feaade8047339528fb066ec5f8a49'
         '3bc2a85a61beb63fc22dcca5168df39d'
         '8c64ebebaff7fa23db8bef3aa54fce9f'
         '4df100e21d454c7241e1d8cd02ccfed6'
         'b20d89547474d4add85158e7bc1ac30e'
         '2112afa616b83fae3c467bd9f36a6577'
         '38a3bd74f6def65d65e1031aed2f6132'
         '366b1007ecc5e72324ead3d53abe6d7f'
         'ad1625291f5831927a066446eb7d6497'
         '7d22d05163027d5a35039ffc426a746d'
         '98eb2eada163f86923622b9fdc260779')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  for i in ${_patches[@]}; do
    # msg2 "Applying ${i}"
    git apply ../${i}
  done

  cat "${srcdir}/config-${pkgver}" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  make prepare
  env DTC_FLAGS=--symbols make dtbs
}

package() {
  cd "${srcdir}/${_srcname}"

  local -a _dtbs=(
    'sun8i-h2-plus-orangepi-zero.dtb'
    'sun8i-h3-orangepi-one.dtb'
    'sun8i-h2-plus-orangepi-r1.dtb'
    'sun8i-h3-nanopi-neo.dtb'
    'sun4i-a10-olinuxino-lime.dtb'
    'sun7i-a20-olinuxino-lime.dtb'
    'sun8i-h2-plus-bananapi-m2-zero.dtb'
    'sun8i-h2-plus-bananapi-p2-zero.dtb'
  )

  local _dstdir=usr/share/${pkgname}

  mkdir -p "${pkgdir}/${_dstdir}"

  for i in ${_dtbs[@]}; do
    cp arch/arm/boot/dts/${i} "${pkgdir}/${_dstdir}"
  done
}

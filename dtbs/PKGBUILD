# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgbase='sunxi-dtbs'
_srcname=linux-4.16
pkgname=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs')
pkgver=4.16
pkgrel=2
arch=('armv7h')
url="http://www.kernel.org/"
license=('GPL2')
depends=("linux>=${pkgver}.0")
makedepends=('bc' 'git')
options=('!strip')

_rcnrel=armv7-x0
_cfgrel=88ed719293e141e6b5bb0b3d1f9f68789be5351f

source=("http://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
	"http://rcn-ee.com/deb/sid-armhf/v${pkgver}.1-${_rcnrel}/patch-${pkgver%}.1-${_rcnrel}.diff.gz"
	'0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch'
	'0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch'
	'0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch'
	'0004-arm-dts-sunxi-H3-NanoPi-add-missing-ethernet0-alias.patch'
	"http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-armv7/config")
md5sums=('1357fb4ee7c288fdeac5d4e0048f5c18'
         'a25c6c74c5e7ffd78408a1d3960a8a27'
         'e09ae942ae43476c8b86aaaf8f26fb32'
         '9e377c377df54d0b083a706d2c9ac98d'
         '17ab94208d65a41a9d0a9cda5cedbfd7'
         'a283cad32e5f41562e3b2d9c862aec89'
         '1a7b42917c194b12fc265b6b2ad38a3e')

prepare() {
  cd "${srcdir}/${_srcname}"

  # RCN patch
  git apply ../patch-${pkgver%}.1-${_rcnrel}.diff

  # custom patches
  git apply ../0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch
  git apply ../0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch
  git apply ../0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch
  git apply ../0004-arm-dts-sunxi-H3-NanoPi-add-missing-ethernet0-alias.patch

  cat "${srcdir}/config" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  export DTC_FLAGS=--symbols

  make prepare
  make dtbs
}

package_orangepi-dtbs() {
  pkgdesc='Additional DTBs for Orange Pi boards'
  provides=('sunxi-dtbs')
  conflicts=('sunxi-dtbs')

  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun8i-h2-plus-orangepi-zero.dtb "${pkgdir}/boot/dtbs-extra"
}

package_nanopi-dtbs() {
  pkgdesc='Additional DTBs for NanoPi boards'
  provides=('sunxi-dtbs')
  conflicts=('sunxi-dtbs')

  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun8i-h3-nanopi-neo.dtb "${pkgdir}/boot/dtbs-extra"
}

package_olinuxino-lime-dtbs() {
  pkgdesc='Additional DTBs for oLinuXino Lime boards'
  provides=('sunxi-dtbs')
  conflicts=('sunxi-dtbs')

  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun4i-a10-olinuxino-lime.dtb "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun7i-a20-olinuxino-lime.dtb "${pkgdir}/boot/dtbs-extra"
}

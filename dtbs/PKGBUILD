# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname='sunxi-dtbs'
_srcname=linux-5.1
pkgdesc='Additional DTBs for Allwinder-based boards'
conflicts=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
replaces=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
pkgver=5.1.p1
pkgrel=1
arch=('armv7h')
url="http://www.kernel.org/"
license=('GPL2')
depends=('linux>5.1')
makedepends=('bc' 'git')
options=('!strip')
_cfgrel=20cf7ffd1cee13794f20edd26dfa26c73c8ee979
_patches=(
	'0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch'
	'0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch'
	'0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch'
	'0004-arm-dts-sunxi-NanoPi-Neo-add-missing-ethernet0-alias.patch'
	'0005-arm-dts-sunxi-BananaPi-M2-Zero-update-emac.patch'
	'0006-ARM-dts-sun8i-Enable-HDMI-on-Banana-Pi-M2-Zero-board.patch'
	'0007-arm-dts-sunxi-Add-Banana-Pi-P2-Zero-board.patch'
	'0008-arm-dts-sunxi-OrangePi-Zero-improve-WiFi-bindings.patch'
	'0009-arm-dts-sunxi-OrangePi-R1-remove-correct-node.patch'
)
source=("http://www.kernel.org/pub/linux/kernel/v5.x/${_srcname}.tar.xz"
	${_patches[@]}
	"config-${pkgver}::http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-armv7/config")
md5sums=('15fbdff95ff98483069ac6e215b9f4f9'
         '57bad2a103e08d1fae62bee7251debc9'
         'ada97e1c172cb7e69bec7d274aab0198'
         '88f7cae8ed7fa1d0f1a18fce99393f83'
         'cb451fc3f79e4a57d198e214e697350e'
         'a56e5cbd9c5a30e2743d74abe3979cbf'
         'f6571335a1ad583f338e5a26e5d22bab'
         '2862eac7854c45eca80295d6687b1324'
         '76c8323cf7cf5d02af02807149b8d1e7'
         'af32c64222d811e8f72354d19d34f9f3'
         '174a565035583b04f6aeb8971a2a825d')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  for i in ${_patches[@]}; do
    # msg2 "Applying ${i}"
    git apply ../${i}
  done

  cat "${srcdir}/config-${pkgver}" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  make prepare
  env DTC_FLAGS=--symbols make dtbs
}

package() {
  cd "${srcdir}/${_srcname}"

  local -a _dtbs=(
    'sun8i-h2-plus-orangepi-zero.dtb'
    'sun8i-h3-orangepi-one.dtb'
    'sun8i-h2-plus-orangepi-r1.dtb'
    'sun8i-h3-nanopi-neo.dtb'
    'sun4i-a10-olinuxino-lime.dtb'
    'sun7i-a20-olinuxino-lime.dtb'
    'sun8i-h2-plus-bananapi-m2-zero.dtb'
    'sun8i-h2-plus-bananapi-p2-zero.dtb'
  )

  local _dstdir=usr/share/${pkgname}

  mkdir -p "${pkgdir}/${_dstdir}"

  for i in ${_dtbs[@]}; do
    cp arch/arm/boot/dts/${i} "${pkgdir}/${_dstdir}"
  done
}

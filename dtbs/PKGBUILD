# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgbase='sunxi-dtbs'
_srcname=linux-4.18
pkgname=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs')
pkgver=4.18
pkgrel=1
arch=('armv7h')
url="http://www.kernel.org/"
license=('GPL2')
depends=("linux>=${pkgver}.0")
makedepends=('bc' 'git')
options=('!strip')

_rcnrel=armv7-x2
_cfgrel=92e274f8fe9d016041057754e61b86f7d08dfe0a

source=("http://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
	"http://rcn-ee.com/deb/sid-armhf/v${pkgver}.0-${_rcnrel}/patch-${pkgver%}-${_rcnrel}.diff.gz"
	'0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch'
	'0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch'
	'0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch'
	'0004-arm-dts-sunxi-H3-NanoPi-add-missing-ethernet0-alias.patch'
	"http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-armv7/config")
md5sums=('bee5fe53ee1c3142b8f0c12c0d3348f9'
         '8feb37c8b5788c2cdf1ca31f7d981661'
         '9437ececbc5e83e6076b074ca5bfcb06'
         'd2cce4e885f4527f40e682d90cb2405c'
         '4706839e773023a3b14d2098b8c7a3c6'
         '73485b7b701407cd6765035e7626612b'
         'c28ae95a8fe4a4d4f5bea9229820cde8')

prepare() {
  cd "${srcdir}/${_srcname}"

  # RCN patch
  git apply ../patch-${pkgver%}-${_rcnrel}.diff

  # custom patches
  git apply ../0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch
  git apply ../0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch
  git apply ../0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch
  git apply ../0004-arm-dts-sunxi-H3-NanoPi-add-missing-ethernet0-alias.patch

  cat "${srcdir}/config" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  export DTC_FLAGS=--symbols

  make prepare
  make dtbs
}

package_orangepi-dtbs() {
  pkgdesc='Additional DTBs for Orange Pi boards'
  provides=("sunxi-dtbs=${pkgver}")
  conflicts=('sunxi-dtbs')

  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun8i-h2-plus-orangepi-zero.dtb "${pkgdir}/boot/dtbs-extra"
}

package_nanopi-dtbs() {
  pkgdesc='Additional DTBs for NanoPi boards'
  provides=("sunxi-dtbs=${pkgver}")
  conflicts=('sunxi-dtbs')

  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun8i-h3-nanopi-neo.dtb "${pkgdir}/boot/dtbs-extra"
}

package_olinuxino-lime-dtbs() {
  pkgdesc='Additional DTBs for oLinuXino Lime boards'
  provides=("sunxi-dtbs=${pkgver}")
  conflicts=('sunxi-dtbs')

  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun4i-a10-olinuxino-lime.dtb "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun7i-a20-olinuxino-lime.dtb "${pkgdir}/boot/dtbs-extra"
}

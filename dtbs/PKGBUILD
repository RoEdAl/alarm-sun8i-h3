# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname='sunxi-dtbs'
_srcname=linux-4.20
pkgdesc='Additional DTBs for Allwinder-based boards'
conflicts=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
replaces=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
pkgver=4.20.p1
pkgrel=1
arch=('armv7h')
url="http://www.kernel.org/"
license=('GPL2')
depends=('linux>4.20')
makedepends=('bc' 'git')
options=('!strip')
_cfgrel=b21edcbede10e67392282471784faec6545f9bdc
_patches=(
	'0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch'
	'0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch'
	'0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch'
	'0004-arm-dts-sunxi-NanoPi-Neo-add-missing-ethernet0-alias.patch'
	'0005-arm-dts-sunxi-BananaPi-M2-Zero-update-emac.patch'
	'0006-ARM-dts-sun8i-Enable-HDMI-on-Banana-Pi-M2-Zero-board.patch'
	'0007-arm-dts-sunxi-Add-Banana-Pi-P2-Zero-board.patch'
	'0008-arm-dts-sunxi-OrangePi-Zero-improve-WiFi-bindings.patch'
)
source=("http://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
	${_patches[@]}
	"config-${pkgver}::http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-armv7/config")
md5sums=('d39dd4ba2d5861c54b90d49be19eaf31'
         'f37c32c3dd4bb3be83a6f93fc78833c5'
         'b003bcbaf43e5b2b14571e8b7724b489'
         'fd5e79fc182681ccb3a374651b4ccc0c'
         '350c59f4b878d180bcb9e18c3d28aafc'
         '2e894d2376d90fe2fe1064fd2f7c8795'
         '0b5cf1179362739ab2c9a1c1ec824415'
         '80777a1b15531428c498d6e63b9f0e71'
         'f6ceea8ce11e03220413b3e10dec2045'
         '6b95ea40314c5531cea810e3a16e02f9')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  for i in ${_patches[@]}; do
    # msg2 "Applying ${i}"
    git apply ../${i}
  done

  cat "${srcdir}/config-${pkgver}" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  make prepare
  env DTC_FLAGS=--symbols make dtbs
}

package() {
  cd "${srcdir}/${_srcname}"

  local -a _dtbs=(
    'sun8i-h2-plus-orangepi-zero.dtb'
    'sun8i-h3-orangepi-one.dtb'
    'sun8i-h2-plus-orangepi-r1.dtb'
    'sun8i-h3-nanopi-neo.dtb'
    'sun4i-a10-olinuxino-lime.dtb'
    'sun7i-a20-olinuxino-lime.dtb'
    'sun8i-h2-plus-bananapi-m2-zero.dtb'
    'sun8i-h2-plus-bananapi-p2-zero.dtb'
  )

  local _dstdir=usr/share/${pkgname}

  mkdir -p "${pkgdir}/${_dstdir}"

  for i in ${_dtbs[@]}; do
    cp arch/arm/boot/dts/${i} "${pkgdir}/${_dstdir}"
  done
}

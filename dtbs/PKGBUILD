# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname='sunxi-dtbs'
_srcname=linux-4.19
pkgdesc='Additional DTBs for Allwinder-based boards'
conflicts=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
replaces=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
pkgver=4.19.0
pkgrel=1
arch=('armv7h')
url="http://www.kernel.org/"
license=('GPL2')
depends=("linux>=${pkgver}.0")
makedepends=('bc' 'git')
options=('!strip')
_cfgrel=13278c7fc5a06de728b37446bfec34fd9e6e85a0
source=("http://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
	'0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch'
	'0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch'
	'0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch'
	'0004-arm-dts-sunxi-NanoPi-Neo-add-missing-ethernet0-alias.patch'
	'0005-arm-dts-sunxi-BananaPi-M2-Zero-update-emac.patch'
	'0006-ARM-dts-sun8i-Enable-HDMI-on-Banana-Pi-M2-Zero-board.patch'
	'0007-arm-dts-sunxi-Add-Banana-Pi-P2-Zero-board.patch'
	"config-${pkgver}::http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-armv7/config")
md5sums=('740a90cf810c2105df8ee12e5d0bb900'
         '0c868268654fa7d49e94a546760c9ad8'
         'd8976700cabc184392754cc771f804de'
         '802d82412e3a9142e8a0e3068185bf7d'
         'd43a270c82f62c3522362eaeda519923'
         'bc53edd528386e7c6b150e90a14ccf3a'
         '91dd1813917f1254d844a03d226a1712'
         '0da757efef39d04f5468fa759efe531b'
         'e7d793a9a2b813a58392a4fae7e414c3')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  git apply ../0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch
  git apply ../0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch
  git apply ../0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch
  git apply ../0004-arm-dts-sunxi-NanoPi-Neo-add-missing-ethernet0-alias.patch
  git apply ../0005-arm-dts-sunxi-BananaPi-M2-Zero-update-emac.patch
  git apply ../0006-ARM-dts-sun8i-Enable-HDMI-on-Banana-Pi-M2-Zero-board.patch
  git apply ../0007-arm-dts-sunxi-Add-Banana-Pi-P2-Zero-board.patch

  cat "${srcdir}/config-${pkgver}" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  make prepare
  env DTC_FLAGS=--symbols make dtbs
}

package() {
  cd "${srcdir}/${_srcname}"

  local _dstdir=usr/share/${pkgname}
  mkdir -p "${pkgdir}/${_dstdir}"

  cp arch/arm/boot/dts/sun8i-h2-plus-orangepi-zero.dtb "${pkgdir}/${_dstdir}"
  cp arch/arm/boot/dts/sun8i-h3-nanopi-neo.dtb "${pkgdir}/${_dstdir}"
  cp arch/arm/boot/dts/sun4i-a10-olinuxino-lime.dtb "${pkgdir}/${_dstdir}"
  cp arch/arm/boot/dts/sun7i-a20-olinuxino-lime.dtb "${pkgdir}/${_dstdir}"
  cp arch/arm/boot/dts/sun8i-h2-plus-bananapi-m2-zero.dtb "${pkgdir}/${_dstdir}"
  cp arch/arm/boot/dts/sun8i-h2-plus-bananapi-p2-zero.dtb "${pkgdir}/${_dstdir}"
}

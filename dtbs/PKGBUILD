# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname='sunxi-dtbs'
_srcname=linux-5.4
pkgdesc='Additional DTBs for Allwinder-based boards'
conflicts=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
replaces=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs' 'bananapi-dtbs')
pkgver=5.4.p1
pkgrel=1
arch=('armv7h')
url="http://www.kernel.org/"
license=('GPL2')
depends=('linux>5.1')
makedepends=('bc' 'git')
options=('!strip')
_cfgrel=e09abb00ffa3451f6c6a5845254bf23a211d6a1f
_patches=(
	'0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch'
	'0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch'
	'0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch'
	'0004-arm-dts-sunxi-NanoPi-Neo-add-missing-ethernet0-alias.patch'
	'0005-arm-dts-sunxi-BananaPi-M2-Zero-update-emac.patch'
	'0006-ARM-dts-sun8i-Enable-HDMI-on-Banana-Pi-M2-Zero-board.patch'
	'0007-arm-dts-sunxi-Add-Banana-Pi-P2-Zero-board.patch'
	'0008-arm-dts-sunxi-OrangePi-Zero-improve-WiFi-bindings.patch'
	'0009-arm-dts-sunxi-OrangePi-R1-remove-correct-node.patch'
)
source=("http://www.kernel.org/pub/linux/kernel/v5.x/${_srcname}.tar.xz"
	${_patches[@]}
	"config-${pkgver}::http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-armv7/config")
md5sums=('ce9b2d974d27408a61c53a30d3f98fb9'
         '4131800ef3acba2ba3e007a195c3c36f'
         '5d3c8c812626860fff857844fb14753a'
         '3d21077f81bc4cd3216fd17b8ab03ebc'
         'a3208ac0ab9a956bcd04f59822b945da'
         '394804969ae8d0aa6f020e23df8792e7'
         '0840896dc77277329f9d812667bdeced'
         '678a6ed87164d7e1943e03f9a2c37114'
         '1018536df680ab71afab86313126b721'
         '06595d1cbdfc8db166ac517033e6fa9b'
         '38953deeb5f01372bb6b6ed14d8660a9')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  for i in ${_patches[@]}; do
    # msg2 "Applying ${i}"
    git apply ../${i}
  done

  cat "${srcdir}/config-${pkgver}" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  make prepare
  env DTC_FLAGS=--symbols make dtbs
}

package() {
  cd "${srcdir}/${_srcname}"

  local -a _dtbs=(
    'sun8i-h2-plus-orangepi-zero.dtb'
    'sun8i-h3-orangepi-one.dtb'
    'sun8i-h2-plus-orangepi-r1.dtb'
    'sun8i-h3-nanopi-neo.dtb'
    'sun4i-a10-olinuxino-lime.dtb'
    'sun7i-a20-olinuxino-lime.dtb'
    'sun8i-h2-plus-bananapi-m2-zero.dtb'
    'sun8i-h2-plus-bananapi-p2-zero.dtb'
  )

  local _dstdir=usr/share/${pkgname}

  mkdir -p "${pkgdir}/${_dstdir}"

  for i in ${_dtbs[@]}; do
    cp arch/arm/boot/dts/${i} "${pkgdir}/${_dstdir}"
  done
}

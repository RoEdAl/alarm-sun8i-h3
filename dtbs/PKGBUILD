# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgbase='sunxi-dtbs'
_srcname=linux-4.19
pkgname=('orangepi-dtbs' 'nanopi-dtbs' 'olinuxino-lime-dtbs')
pkgver=4.19
pkgrel=1
arch=('armv7h')
url="http://www.kernel.org/"
license=('GPL2')
depends=("linux>=${pkgver}.0")
makedepends=('bc' 'git')
options=('!strip')
_cfgrel=13278c7fc5a06de728b37446bfec34fd9e6e85a0
source=("http://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
	'0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch'
	'0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch'
	'0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch'
	'0004-arm-dts-sunxi-H3-NanoPi-add-missing-ethernet0-alias.patch'
	"config-${pkgver}::http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-armv7/config")
md5sums=('740a90cf810c2105df8ee12e5d0bb900'
         '8f04072da5f943d122a4125c43a900d5'
         '8dfc2c98e7dde0c50c61edd04258a399'
         '3d13b71c4776d131b3fb5be75d2c7cbe'
         '3fc0ad1e2f1ca66dc0be545500d910b9'
         'e7d793a9a2b813a58392a4fae7e414c3')

prepare() {
  cd "${srcdir}/${_srcname}"

  # custom patches
  git apply ../0001-arm-dts-sunxi-NanoPi-Neo-status-LED-without-defaulte.patch
  git apply ../0002-arm-dts-sunxi-Orange-Pi-Zero-simplification.patch
  git apply ../0003-ARM-dts-sun4i-Correct-pinctrl-nodes.patch
  git apply ../0004-arm-dts-sunxi-H3-NanoPi-add-missing-ethernet0-alias.patch

  cat "${srcdir}/config-${pkgver}" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  export DTC_FLAGS=--symbols

  make prepare
  make dtbs
}

package_orangepi-dtbs() {
  pkgdesc='Additional DTBs for Orange Pi boards'
  provides=("sunxi-dtbs=${pkgver}")
  conflicts=('sunxi-dtbs')

  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun8i-h2-plus-orangepi-zero.dtb "${pkgdir}/boot/dtbs-extra"
}

package_nanopi-dtbs() {
  pkgdesc='Additional DTBs for NanoPi boards'
  provides=("sunxi-dtbs=${pkgver}")
  conflicts=('sunxi-dtbs')

  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun8i-h3-nanopi-neo.dtb "${pkgdir}/boot/dtbs-extra"
}

package_olinuxino-lime-dtbs() {
  pkgdesc='Additional DTBs for oLinuXino Lime boards'
  provides=("sunxi-dtbs=${pkgver}")
  conflicts=('sunxi-dtbs')

  cd "${srcdir}/${_srcname}"

  mkdir -p "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun4i-a10-olinuxino-lime.dtb "${pkgdir}/boot/dtbs-extra"
  cp arch/arm/boot/dts/sun7i-a20-olinuxino-lime.dtb "${pkgdir}/boot/dtbs-extra"
}

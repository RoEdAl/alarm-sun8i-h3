# U-Boot: sunXi (OrangePi/NanoPi)

pkgbase='uboot-sun8i-h3'
pkgname=('uboot-orangepi-zero' 'uboot-nanopi-neo')
pkgver=2018.01
pkgrel=4
arch=('armv7h')
_baserel=62ceb441bfe96eba0aa467c07516df8c5947aeeb
url="http://git.denx.de/u-boot.git/"
license=('GPL')
options=('!strip')
makedepends=('bc' 'dtc' 'python2' 'swig')
backup=(
	'boot/boot.txt'
	'boot/boot.scr')
optdepends=('uboot-tools' 'sun8i-h3-dt-overlays')

source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
	'boot.txt'
	'flash-uboot.sh'
	"http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_baserel}/alarm/uboot-sunxi/mkscr"
	'orangepi_zero_defconfig'
	'nanopi_neo_defconfig')
md5sums=('b42e45813369f4ae84490a481e531768'
         '0d3396477ed84ebb147475acdd0e337c'
         '4a520897a21351e83b55a7ed8d12b6cb'
         '021623a04afd29ac3f368977140cfbfd'
         'e0131bdea1c554796e242d616238e5d9'
         'c11863316b7761768aa3aa727eca0144')

boards=('orangepi_zero' 'nanopi_neo')

prepare() {
  cd u-boot-${pkgver}

  cp ${srcdir}/orangepi_zero_defconfig configs
  cp arch/arm/dts/sun8i-h2-plus-orangepi-zero.dts arch/arm/dts/sun8i-h2-plus-orangepi-zero_.dts

  cp ${srcdir}/nanopi_neo_defconfig configs
  cp arch/arm/dts/sun8i-h3-nanopi-neo.dts arch/arm/dts/sun8i-h3-nanopi-neo_.dts
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS CXXFLAGS LDFLAGS

  for i in ${boards[@]}; do
    mkdir ../bin_${i}
    make distclean
    make ${i}_config
    echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
    echo 'CONFIG_OF_LIBFDT_OVERLAY=y' >> .config
    make EXTRAVERSION=-${pkgrel} PYTHON=python2 DTC=/usr/bin/dtc
    mv u-boot-sunxi-with-spl.bin ../bin_${i}
  done

  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d ../boot.txt ../boot.scr
}

package_uboot-orangepi-zero() {
  pkgdesc='U-Boot for Orange Pi Zero'
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi')
  depends=('linux-armv7-rc' 'orangepi-dtbs')

  install -d "${pkgdir}/boot"
  install -Dm644 bin_orangepi_zero/u-boot-sunxi-with-spl.bin "${pkgdir}/boot"

  install -Dm644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm755 mkscr "${pkgdir}/boot/mkscr"
  install -Dm755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"
}

package_uboot-nanopi-neo() {
  pkgdesc='U-Boot for NanoPi Neo'
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi')
  depends=('linux-armv7-rc' 'nanopi-dtbs')

  install -d "${pkgdir}/boot"
  install -Dm644 bin_nanopi_neo/u-boot-sunxi-with-spl.bin "${pkgdir}/boot"

  install -Dm644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm755 mkscr "${pkgdir}/boot/mkscr"
  install -Dm755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"
}

# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgbase='uboot-sunxi-dto'
pkgname=('uboot-orangepi-zero' 'uboot-nanopi-neo' 'uboot-a10-olinuxino-lime-dto' 'uboot-a20-olinuxino-lime-dto' 'uboot-bananapi-m2-zero' 'uboot-bananapi-p2-zero')
pkgver=2018.11
pkgrel=2
arch=('armv7h')
_baserel=62ceb441bfe96eba0aa467c07516df8c5947aeeb
url='http://git.denx.de/u-boot.git/'
license=('GPL')
options=('!strip')
makedepends=('bc' 'python2' 'swig' 'dtc' 'sunxi-dtbs>4.19')
backup=('boot/boot.txt'
	'boot/boot.scr')
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
	'boot.txt'
	'flash-uboot.sh'
	'config-fragment'
	"http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_baserel}/alarm/uboot-sunxi/mkscr"
	'0001-arm-dts-sunxi-H3-NanoPi-add-missing-ethernet0-alias.patch'
	'0002-Add-support-for-Banana-Pi-M2-Zero-board.patch'
	'0003-Update-configuration-for-Orange-Pi-Zero.patch'
	'0004-ARM-dts-sun8i-Enable-HDMI-on-Banana-Pi-M2-Zero-board.patch'
	'0005-Add-support-for-Banana-Pi-P2-Zero-board.patch')
sha1sums=('e58e86c633ad3e2613196e0c0b0a7760ae5966d6'
          '4088ae725c22b5468deece9c72eff6b0470a3128'
          'a49a8c18e1b0f920a430cf57990c0c42dc864f59'
          'ec687528b35f159c68d9d44cf481d223a895ebb8'
          '008b9ce4fcf64c939868148ca387fa733c8b7e69'
          'cadea6310292e0723b20487dd4ffa0b852121225'
          'b23db8f444a3fb75a477843165265c1d44d1bf1e'
          'a1c8b81d85d51b9eb7007e053c306268f1932f25'
          'f67ed397fd1ccb2926314ef96d5925d28266c450'
          'aff55a58163dbd4cb2e404c27fd280ba06d576d3')

_boards=('orangepi_zero' 'nanopi_neo' 'A10-OLinuXino-Lime' 'A20-OLinuXino-Lime' 'Sinovoip_BPI_M2_Zero' 'Sinovoip_BPI_P2_Zero')

prepare() {
  cd u-boot-${pkgver}

  git apply ../0001-arm-dts-sunxi-H3-NanoPi-add-missing-ethernet0-alias.patch
  git apply ../0002-Add-support-for-Banana-Pi-M2-Zero-board.patch
  git apply ../0003-Update-configuration-for-Orange-Pi-Zero.patch
  git apply ../0004-ARM-dts-sun8i-Enable-HDMI-on-Banana-Pi-M2-Zero-board.patch
  git apply ../0005-Add-support-for-Banana-Pi-P2-Zero-board.patch
}

build() {
  cd u-boot-${pkgver}

  for i in ${_boards[@]}; do
    msg2 ${i}
    mkdir ../bin_${i} || true
    make distclean
    make ${i}_config
    scripts/kconfig/merge_config.sh .config ${srcdir}/config-fragment
    make EXTRAVERSION=-${pkgrel} PYTHON=python2 V=0
    mv u-boot-sunxi-with-spl.bin ../bin_${i}
  done

  tools/mkimage -A arm -O linux -T script -C none -n 'U-Boot boot script' -d ../boot.txt ../boot.scr
}

package_uboot-orangepi-zero() {
  pkgdesc='U-Boot for Orange Pi Zero (with device tree overlays support)'
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi' 'orangepi-dtbs')
  optdepends=('uboot-tools' 'sun8i-h3-dt-overlays')

  install -d "${pkgdir}/boot/dtbs-extra"
  install -Dm644 bin_orangepi_zero/u-boot-sunxi-with-spl.bin "${pkgdir}/boot/u-boot-sunxi-with-spl.bin"
  install -Dm644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm755 mkscr "${pkgdir}/boot/mkscr"
  install -Dm755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"
  install -Dm644 /usr/share/sunxi-dtbs/sun8i-h2-plus-orangepi-zero.dtb "${pkgdir}/boot/dtbs-extra/sun8i-h2-plus-orangepi-zero.dtb"
}

package_uboot-nanopi-neo() {
  pkgdesc='U-Boot for NanoPi Neo (with device tree overlays support)'
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi' 'nanopi-dtbs')
  optdepends=('uboot-tools' 'sun8i-h3-dt-overlays')

  install -d "${pkgdir}/boot/dtbs-extra"
  install -Dm644 bin_nanopi_neo/u-boot-sunxi-with-spl.bin "${pkgdir}/boot/u-boot-sunxi-with-spl.bin"
  install -Dm644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm755 mkscr "${pkgdir}/boot/mkscr"
  install -Dm755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"
  install -Dm644 /usr/share/sunxi-dtbs/sun8i-h3-nanopi-neo.dtb "${pkgdir}/boot/dtbs-extra/sun8i-h3-nanopi-neo.dtb"
}

package_uboot-a10-olinuxino-lime-dto() {
  pkgdesc='U-Boot for A10 OLinuXino Lime (with device tree overlays support)'
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi' 'olinuxino-lime-dtbs')
  optdepends=('uboot-tools' 'sun4i-a10-dt-overlays')

  install -d "${pkgdir}/boot/dtbs-extra"
  install -Dm644 bin_A10-OLinuXino-Lime/u-boot-sunxi-with-spl.bin "${pkgdir}/boot/u-boot-sunxi-with-spl.bin"
  install -Dm644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm755 mkscr "${pkgdir}/boot/mkscr"
  install -Dm755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"
  install -Dm644 /usr/share/sunxi-dtbs/sun4i-a10-olinuxino-lime.dtb "${pkgdir}/boot/dtbs-extra/sun4i-a10-olinuxino-lime.dtb"
}

package_uboot-a20-olinuxino-lime-dto() {
  pkgdesc='U-Boot for A20 OLinuXino Lime (with device tree overlays support)'
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi' 'olinuxino-lime-dtbs')
  optdepends=('uboot-tools' 'sun7i-a20-dt-overlays')

  install -d "${pkgdir}/boot/dtbs-extra"
  install -Dm644 bin_A20-OLinuXino-Lime/u-boot-sunxi-with-spl.bin "${pkgdir}/boot/u-boot-sunxi-with-spl.bin"
  install -Dm644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm755 mkscr "${pkgdir}/boot/mkscr"
  install -Dm755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"
  install -Dm644 /usr/share/sunxi-dtbs/sun7i-a20-olinuxino-lime.dtb "${pkgdir}/boot/dtbs-extra/sun7i-a20-olinuxino-lime.dtb"
}

package_uboot-bananapi-m2-zero() {
  pkgdesc='U-Boot for Banana Pi M2 Zero (with device tree overlays support)'
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi' 'bananapi-dtbs')
  optdepends=('uboot-tools' 'sun8i-h3-dt-overlays')

  install -d "${pkgdir}/boot/dtbs-extra"
  install -Dm644 bin_Sinovoip_BPI_M2_Zero/u-boot-sunxi-with-spl.bin "${pkgdir}/boot/u-boot-sunxi-with-spl.bin"
  install -Dm644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm755 mkscr "${pkgdir}/boot/mkscr"
  install -Dm755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"
  install -Dm644 /usr/share/sunxi-dtbs/sun8i-h2-plus-bananapi-m2-zero.dtb "${pkgdir}/boot/dtbs-extra/sun8i-h2-plus-bananapi-m2-zero.dtb"
}

package_uboot-bananapi-p2-zero() {
  pkgdesc='U-Boot for Banana Pi P2 Zero (with device tree overlays support)'
  install=${pkgbase}.install
  provides=('uboot-sunxi')
  conflicts=('uboot-sunxi' 'bananapi-dtbs')
  optdepends=('uboot-tools' 'sun8i-h3-dt-overlays')

  install -d "${pkgdir}/boot/dtbs-extra"
  install -Dm644 bin_Sinovoip_BPI_P2_Zero/u-boot-sunxi-with-spl.bin "${pkgdir}/boot/u-boot-sunxi-with-spl.bin"
  install -Dm644 boot.txt "${pkgdir}/boot/boot.txt"
  install -Dm644 boot.scr "${pkgdir}/boot/boot.scr"
  install -Dm755 mkscr "${pkgdir}/boot/mkscr"
  install -Dm755 flash-uboot.sh "${pkgdir}/boot/flash-uboot"
  install -Dm644 /usr/share/sunxi-dtbs/sun8i-h2-plus-bananapi-p2-zero.dtb "${pkgdir}/boot/dtbs-extra/sun8i-h2-plus-bananapi-p2-zero.dtb"
}
